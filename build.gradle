plugins {
    id 'application'
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

application {
    mainClassName = "radish.batch.Radish"
}

group 'it.giovanniberti.radish'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenCentral()
}

class HBaseAlignmentRule implements ComponentMetadataRule {
    void execute(ComponentMetadataContext ctx) {
        ctx.details.with {
            if (id.group.startsWith("org.apache.hbase")) {
                belongsTo("org.apache.hbase:hbase-virtual-platform:2.2.3")
            }
        }
    }
}

class StormAlignmentRule implements ComponentMetadataRule {
    void execute(ComponentMetadataContext ctx) {
        ctx.details.with {
            if (id.group.startsWith("org.apache.storm")) {
                belongsTo("org.apache.storm:storm-virtual-platform:2.1.0")
            }
        }
    }
}

configurations.all {
    exclude group: 'org.slf4j", module: "slf4j-simple'
    exclude group: 'org.slf4j', module: 'slf4j-api'
    exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
}

dependencies {
    components.all(HBaseAlignmentRule)
    components.all(StormAlignmentRule)

    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation "io.github.cdimascio:java-dotenv:5.1.3"
    implementation group: 'org.apache.hbase', name: 'hbase-client', version: '2.2.3'
    implementation group: 'org.apache.hbase', name: 'hbase-mapreduce', version: '2.2.3'
    implementation files("./LIRE-1.0_b04.jar")
    implementation group: "org.apache.storm", name: "storm-core", version: "2.1.0"
    implementation group: "org.apache.storm", name: "storm-hbase", version: "2.1.0"
    implementation group: "org.twitter4j", name: "twitter4j-core", version: "4.0.4"
    implementation group: "org.twitter4j", name: "twitter4j-stream", version: "4.0.4"
    implementation group: 'ch.qos.logback', name: "logback-classic", version: "1.2.3"
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'
    implementation group: "org.apache.commons", name: "commons-math3", version: "3.6.1"
}

sourceSets.main.java.srcDirs = ['src/main/java']

shadowJar {
    zip64 true
    exclude "defaults.yaml"

    dependencies {
        exclude(dependency('org.apache.logging.log4j::'))
        exclude(dependency('org.slf4j:slf4j-simple:'))
        exclude(dependency('org.slf4j:slf4j-api:'))
        exclude(dependency('org.slf4j::'))
    }
}